import ssao.SSAO
import exposure.Exposure
import bloom.Bloom
import godray.Godray
import water.Water

compositor Frame
{
	texture sceneTexture target_size Albedo:DXGI_FORMAT_R8G8B8A8_UNORM Normal:DXGI_FORMAT_R16G16B16A16_SNORM MotionVectors:DXGI_FORMAT_R16G16_FLOAT Depth


	texture sceneVCT target_size DXGI_FORMAT_R16G16B16A16_FLOAT
	alias sceneTextureTmp2 sceneVCT
	texture sceneVCTLow target_size_div 2 DXGI_FORMAT_R16G16B16A16_FLOAT
	texture sceneTextureTmp target_size DXGI_FORMAT_R16G16B16A16_FLOAT

	texture depthDownsample2 target_size_div 2 DXGI_FORMAT_R32_FLOAT

#if UPSCALE
	texture motionVectors target_size DXGI_FORMAT_R16G16_FLOAT
	rwtexture sceneTextureTmp_upscaled output_size DXGI_FORMAT_R16G16B16A16_FLOAT
#else
	texture sceneTextureTmpOut output_size DXGI_FORMAT_R16G16B16A16_FLOAT
#endif

	texture voxelDummy 128 128 DXGI_FORMAT_R8G8B8A8_UNORM

	task PrepareFrame

	task SceneRender.EarlyZ
	{
		after PrepareFrame

		out sceneTexture:Depth

		sync_signal Depth
	}

	task VoxelScene.Bounces (async_compute)
	{
		sync_signal Voxels
	}

	task PrepareFrame.Water (async_compute)
	{
		sync_signal Water
	}

	ref SSAO(sceneTexture:Depth)

	task Shadows
	{
		after PrepareFrame
	}

	task VoxelScene.Voxelize
	{
		after Shadows
		sync_wait Voxels

		out voxelDummy
	}

#if WIREFRAME
	task SceneRender.Wireframe
	{
		sync_wait SSAO
		out sceneTexture
	}
	pass WireframePassThrough
	{
		material PassThrough
		in sceneTexture:Albedo
		out sceneTextureTmp
	}
#else
	task SceneRender.Opaque
	{
		sync_wait SSAO

		out sceneTexture
	}

	pass DeferredVCT
	{
		after VoxelScene.Voxelize

		material DeferredVCT
		in sceneTexture:Normal
		in sceneTexture:Depth
		out sceneVCTLow
	}

	pass UpsampleDeferredVCT
	{
		material UpsampleDepthAware
		in sceneVCTLow
		in SSAO.linearDepth
		in SSAO.linearDepthDownsample2
		in sceneTexture:Normal
		out sceneVCT
	}

	pass DeferredLighting
	{
		material DeferredLighting
		in sceneTexture:Albedo
		in sceneTexture:Normal
		in sceneTexture:Depth
		in SSAO.aoSmooth
		in sceneVCT
		out sceneTextureTmp
	}

	task SceneRender.Forward
	{
		out sceneTextureTmp sceneTexture:Depth
	}
#endif

	pass DepthDownsample
	{
		material LoadThrough
		in sceneTexture:Depth
		out depthDownsample2
	}

	ref Water(sceneTextureTmp,sceneTexture:Depth,depthDownsample2,sceneTextureTmp2)

	ref Exposure(sceneTextureTmp2)

	task SceneRender.Editor
	{
		out sceneTextureTmp2 sceneTexture:Depth (depth_read)
	}

#if UPSCALE
	task Upscale.Prepare

	pass MotionVectors
	{
		material MotionVectors
		in sceneTexture:Depth
		in sceneTexture:MotionVectors
		out motionVectors
	}

	task Upscale (compute)
	{
		in sceneTextureTmp2
		in motionVectors
		in sceneTexture:Depth
		out sceneTextureTmp_upscaled
	}

	ref Bloom(sceneTextureTmp_upscaled,Exposure.exposure)
	ref Godray(sceneTextureTmp_upscaled,sceneTexture:Depth)

	pass ToneMapping
	{
		material ToneMapping
		in sceneTextureTmp_upscaled
		in Bloom.bloom
		in Exposure.exposure
		in Godray.godray
		out Output
	}
#else

	ref Bloom(sceneTextureTmp2,Exposure.exposure)
	ref Godray(sceneTextureTmp2,sceneTexture:Depth)

	pass Fxaa_Luma
 	{
		material FXAA_Luma
		in sceneTextureTmp2
 		out sceneTextureTmpOut
 	}
	pass Fxaa
 	{
		material FXAA
		in sceneTextureTmpOut
 		out sceneTextureTmp2
 	}

	pass ToneMapping
	{
		material ToneMapping
		in sceneTextureTmp2
		in Bloom.bloom
		in Exposure.exposure
		in Godray.godray
		out Output
	}

#endif

	task SceneRender.Debug
	{
		out Output
	}

	task DebugOverlay
	{
		out Output
	}

	task VoxelScene.EndFrame
}
