import ssao.SSAO
import exposure.Exposure
import bloom.Bloom
import godray.Godray

compositor Frame
{
	texture sceneTexture target_size Albedo:DXGI_FORMAT_R8G8B8A8_UNORM Normal:DXGI_FORMAT_R16G16B16A16_FLOAT Distance:DXGI_FORMAT_R32_FLOAT MotionVectors:DXGI_FORMAT_R16G16_FLOAT Depth
	texture sceneVCT target_size DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneVCTLow target_size_div 2 DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneTextureTmp target_size DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneTextureTmpOut output_size DXGI_FORMAT_R8G8B8A8_UNORM

	texture sceneDownscale2 target_size_div 2 DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneDownscale4 target_size_div 8 DXGI_FORMAT_R8G8B8A8_UNORM

#if UPSCALE
	texture motionVectors target_size DXGI_FORMAT_R16G16_FLOAT
	rwtexture sceneTextureTmp_upscaled output_size DXGI_FORMAT_R8G8B8A8_UNORM
#endif

	texture voxelDummy 128 128 DXGI_FORMAT_R8G8B8A8_UNORM

	task PrepareFrame
	{
	}

	task Shadows
	{
		after PrepareFrame
	}

	task VoxelScene
	{
		after Shadows
		target voxelDummy
	}

	task SceneRender
	{
		entry EarlyZ
		target sceneTexture:Depth
	}

	task SceneRender
	{
		after VoxelScene
		entry Opaque
		target sceneTexture
	}

	ref SSAO(sceneTexture:Depth)

	pass DeferredVCT
	{
		material DeferredVCT
		input sceneTexture:Normal
		input sceneTexture:Depth
		target sceneVCTLow
	}

	pass UpsampleDeferredVCT
	{
		material UpsampleDepthAware
		input sceneVCTLow
		input sceneTexture:Depth
		target sceneVCT
	}

	pass DeferredLighting
	{
		material DeferredLighting
		input sceneTexture:Albedo
		input sceneTexture:Normal
		input sceneTexture:Distance
		input sceneTexture:Depth
		input SSAO.aoSmooth
		input sceneVCT
		target sceneTextureTmp
	}

	pass SceneDownsample
	{
		material PassThrough
		input sceneTextureTmp
		target sceneDownscale2
	}
	pass SceneDownsample
	{
		material PassThrough
		input sceneDownscale2
		target sceneDownscale4
	}

	ref Exposure(sceneTextureTmp)

	ref Bloom(sceneDownscale4,Exposure.exposure)

	task SceneRender
	{
		entry Editor
		target sceneTextureTmp sceneTexture:Depth (depth_read)
	}

#if UPSCALE
	task UpscalePrepare

	pass MotionVectors
	{
		material MotionVectors
		input sceneTexture:Depth
		input sceneTexture:MotionVectors
		target motionVectors
	}

	task Upscale (compute_shader)
	{
		input sceneTextureTmp
		input motionVectors
		input sceneTexture:Depth
		input sceneTextureTmp_upscaled
	}

	ref Godray(sceneTextureTmp_upscaled,sceneTexture:Depth)

	pass ToneMapping
	{
		material ToneMappingNoLuma
		input sceneTextureTmp_upscaled
		input Bloom.bloomTexture
		input Exposure.exposure
		input Godray.godray
		target Output
	}
#else
	ref Godray(sceneTextureTmp,sceneTexture:Depth)

	pass ToneMapping
	{
		material ToneMapping
		input sceneTextureTmp
		input Bloom.bloomTexture
		input Exposure.exposure
		input Godray.godray
		target sceneTextureTmpOut
	}

	pass Fxaa
 	{
		material FXAA
		input sceneTextureTmpOut
 		target Output
 	}
#endif

	task DebugOverlay
	{
		target Output
	}
}
