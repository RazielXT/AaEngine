import ssao
import exposure
import bloom

compositor Frame
{
	texture sceneTexture target_size Color:DXGI_FORMAT_R8G8B8A8_UNORM Normal:DXGI_FORMAT_R10G10B10A2_UNORM Distance:DXGI_FORMAT_R16G16_UNORM MotionVectors:DXGI_FORMAT_R16G16_FLOAT Depth
	texture sceneTransparentTexture target_size Color:DXGI_FORMAT_R8G8B8A8_UNORM Fill:DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneTexture2 target_size DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneTransparentTextureDownscale4 target_size_div 4 DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneTransparentTexture2 target_size DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneColors target_size DXGI_FORMAT_R8G8B8A8_UNORM
	texture motionVectors target_size DXGI_FORMAT_R16G16_FLOAT
	rwtexture sceneTexture2_upscaled output_size DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneTexture3 output_size DXGI_FORMAT_R8G8B8A8_UNORM

	texture sceneDownscale2 target_size_div 2 DXGI_FORMAT_R8G8B8A8_UNORM
	texture sceneDownscale4 target_size_div 8 DXGI_FORMAT_R8G8B8A8_UNORM

	texture depthDownsample2 target_size_div 2 DXGI_FORMAT_R32_FLOAT
	texture depthDownsample4 target_size_div 4 DXGI_FORMAT_R32_FLOAT

	rwtexture depthHiZDownsample4 target_size_div 4 DXGI_FORMAT_R32_FLOAT	
	rwtexture depthHiZDownsample32 target_size_div 32 DXGI_FORMAT_R32_FLOAT

	texture voxelDummy 128 128 DXGI_FORMAT_R8G8B8A8_UNORM

	task VoxelScene
	{
		target voxelDummy
	}

	task Shadows
	{
	}

	task SceneRender
	{
		entry Prepare
	}

	task SceneRender
	{
		entry EarlyZ
		target sceneTexture:Depth
	}

	task SceneRender
	{
		entry Opaque
		target sceneTexture
	}

	task RenderPhysics
	{
		target sceneTexture
	}

	ref SSAO:SSAO

	pass SceneDownsample
	{
		material PassThrough
		input sceneTexture:Color
		target sceneDownscale2
	}
	pass SceneDownsample
	{
		material PassThrough
		input sceneDownscale2
		target sceneDownscale4
	}
	pass DepthDownsample
	{
		material LoadThrough
		input sceneTexture:Depth
		target depthDownsample2
	}
	pass DepthDownsample
	{
		material LoadThrough
		input depthDownsample2
		target depthDownsample4
	}

	task HiZDepthDownsample (compute_shader)
	{
		input linearDepthDownsample2 (read)
		input depthHiZDownsample4
		input depthHiZDownsample32
	}

	pass SSAODarken
	{
		material Darken
		input sceneTexture:Color
		input aoSmooth
		target sceneTexture2
	}

	task SceneRender
	{
		entry Transparent

		input sceneTexture:Distance
		input sceneTexture2
		input linearDepthDownsample2
		input depthHiZDownsample4
		input depthHiZDownsample32
		target sceneTransparentTexture sceneTexture:Depth (depth_read)
	}

	pass ApplySceneWater
	{
		material ApplySceneWater
		input sceneTexture2
		input sceneTransparentTexture
		target sceneColors
	}

	ref Exposure:*

	ref Bloom:*

#if UPSCALE

	task UpscalePrepare

	pass MotionVectors
	{
		material MotionVectors
		input sceneTexture:Depth
		input sceneTexture:MotionVectors
		target motionVectors
	}

	#if DLSS
		task Upscale (compute_shader)
		{
			input sceneColors
			input motionVectors
			input sceneTexture:Depth
			input sceneTexture2_upscaled
		}
	#elif FSR
		task Upscale (compute_shader)
		{
			input sceneColors
			input motionVectors
			input sceneTexture:Depth
			input sceneTexture2_upscaled
		}
	#endif

	pass Combine
	{
		material AddThroughNoLuma
		input sceneTexture2_upscaled
		input bloomTexture
		input exposure
		target Output
	}
	
	task SceneRender
	{
		entry Editor
		target Output sceneTexture:Depth (depth_read)
	}

#else
	pass Combine
	{
		material AddThrough
		input sceneColors
		input bloomTexture
		input exposure
		target sceneTexture3
	}

	task SceneRender
	{
		entry Editor
		target sceneTexture3 sceneTexture:Depth (depth_read)
	}

	pass Fxaa
 	{
		material FXAA
		input sceneTexture3
 		target Output
 	}
#endif

	task DebugOverlay
	{
		target Output
	}

#if STANDALONE
	task Imgui
	{
		target Output
	}
#endif
}
